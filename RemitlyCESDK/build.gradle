buildscript {
    ext.kotlin_version = "1.7.10"
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:1.7.20"
    }
}

apply plugin: "com.android.library"
apply plugin: "org.jetbrains.kotlin.android"
apply plugin: 'maven-publish'
apply plugin: "org.jetbrains.dokka"

ext {
    groupId = "com.remitly"
    artifactId = "cesdk"
    artifactDescription = "Remitly Connected Experiences SDK for Android"
}

android {
    namespace "$groupId.$artifactId"

    compileSdk 33

    defaultConfig {
        versionName VERSION_NAME
        buildConfigField "String", "VERSION_NAME", "\"$versionName\""

        minSdk 21
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    // AndroidX Core
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.browser:browser:1.4.0'
    implementation 'androidx.webkit:webkit:1.4.0'

    // UI Components
    implementation 'com.google.android.material:material:1.6.1'

    // Networking library
    api platform("com.squareup.okhttp3:okhttp-bom:4.10.0")
    implementation "com.squareup.okhttp3:okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor"

    // Observables
    implementation 'io.reactivex:rxjava:1.3.8'
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'io.reactivex:rxkotlin:0.60.0'
    implementation 'io.reactivex:rxjava-async-util:0.21.0'
    implementation 'com.artemzin.rxjava:proguard-rules:1.1.5.0'

    // Tests
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.test:core-ktx:1.5.0'
    testImplementation 'org.mockito:mockito-core:4.8.1'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:4.0.0'
    testImplementation 'org.robolectric:robolectric:4.9'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
}

apply from: "publish.gradle"